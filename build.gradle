plugins {
    id "idea"
    id "org.jetbrains.kotlin.jvm" version "1.7.10"
    id "org.jetbrains.kotlin.plugin.spring" version "1.7.10"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.7.10"
    id "org.jetbrains.kotlin.plugin.noarg" version "1.7.10"
    id "io.spring.dependency-management" version "1.1.0"
    id "org.springframework.boot" version "3.0.4" apply false
    id "org.springframework.cloud.contract" version "4.0.2" apply false
    id "org.asciidoctor.jvm.convert" version "3.3.2" apply false
    id "io.mateo.cxf-codegen" version "1.2.0" apply false
    id "com.google.protobuf" version "0.9.2" apply false
}

ext {
    // Common
    springBootVersion = "3.0.4"
    springContractVersion = "4.0.2"
    springShellVersion = "3.0.1"
    kotlinVersion = "1.7.10"
    jetbrainsAnnotationsVersion = "22.0.0"

    // RESTful
    openApiVersion = "2.1.0"
    springCloudContractVersion = "4.0.2"

    // SOAP
    cxfVersion = "4.0.0"
    jakartaVersion = "4.0.0"

    // gRPC
    grpcVersion = "1.40.1"
    protobufVersion = "3.17.3"
    grpcSpringVersion = "4.5.6"

    // GraphQL
    graphQLJavaVersion = "11.1.0"

    // Test
    postgresContainerVersion = "1.17.6"
}

allprojects { ->
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "io.spring.dependency-management"

    [compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

    repositories {
        mavenCentral()
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            mavenBom "org.springframework.shell:spring-shell-dependencies:$springShellVersion"
            mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:$springCloudContractVersion"
        }
    }

    dependencies {
        implementation "org.apache.commons:commons-lang3"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
        }
    }

    tasks.withType(Jar).all {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}
