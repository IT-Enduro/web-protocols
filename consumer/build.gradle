apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'spring-cloud-contract'
apply plugin: 'maven-publish'
apply plugin: 'org.asciidoctor.convert'

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

contracts {
    baseClassMappings {
        baseClassMapping('.*', 'ru.romanow.protocols.consumer.web.BaseRestTest')
    }
    basePackageForTests = "ru.romanow.protocols.consumer.web"
}

publishing {
    publications {
        stubs(MavenPublication) {
            artifactId project.name
            artifact verifierStubsJar
        }
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

dependencies {
    compile project(":api")

    compile "org.springframework.boot:spring-boot-starter-web"

    testCompile "org.codehaus.groovy:groovy-all"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.springframework.cloud:spring-cloud-contract-spec"
    testCompile "org.springframework.cloud:spring-cloud-starter-contract-verifier"
    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc"
    asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor"
}
