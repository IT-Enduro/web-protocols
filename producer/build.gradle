apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "spring-cloud-contract"
apply plugin: "org.asciidoctor.convert"
apply plugin: "maven-publish"

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

contracts {
    baseClassMappings {
        baseClassMapping ".*operation.*", "ru.romanow.protocols.producer.web.BaseOperationControllerTest"
        baseClassMapping ".*ping.*", "ru.romanow.protocols.producer.web.BasePingControllerTest"
    }
    basePackageForTests = "ru.romanow.protocols.producer.web"
}

publishing {
    publications {
        stubs(MavenPublication) {
            artifactId project.name
            artifact verifierStubsJar
        }
    }
}

ext {
    snippetsDir = file("build/generated-snippets")
    htmlOutputDir = file("build/generated-html")
}

asciidoctor {
    inputs.dir snippetsDir
    sourceDir "src/main/asciidoc"
    outputDir htmlOutputDir

    attributes "snippets": snippetsDir
    dependsOn test
}

dependencies {
    compile project(":api")

    compile "org.springframework.boot:spring-boot-starter-web"

    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.springframework.cloud:spring-cloud-contract-spec"
    testCompile "org.springframework.cloud:spring-cloud-starter-contract-verifier"
    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc"

    asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor"
}

bootJar {
    archiveFileName = "producer.jar"
}