import io.mateo.cxf.codegen.wsdl2java.Wsdl2Java

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.spring"
    id "org.jetbrains.kotlin.plugin.jpa"
    id "org.springframework.boot"
    id "io.mateo.cxf-codegen"
}

cxfCodegen {
    cxfVersion = "4.0.0"
}

tasks.register("generateStubs", Wsdl2Java) {
    toolOptions {
        wsdl.set(file("$projectDir/src/main/resources/wsdl/servers.wsdl"))
        outputDir.set(file("$projectDir/src/main/generated"))
        markGenerated.set(true)
        packageNames.set(["ru.romanow.protocols.soap.client"])
        xjcArgs.add("-Xts")
    }
}

runKtlintCheckOverMainSourceSet.mustRunAfter(generateStubs)

dependencies {
    implementation project(":common-client")

    implementation "org.springframework.boot:spring-boot-starter-web-services"
    implementation "org.springframework.ws:spring-ws-support"

    cxfCodegen "jakarta.xml.ws:jakarta.xml.ws-api"
    cxfCodegen "jakarta.annotation:jakarta.annotation-api"
    cxfCodegen "org.apache.cxf.xjcplugins:cxf-xjc-ts:$jakartaVersion"

    runtimeOnly "com.sun.xml.ws:jaxws-rt:$jakartaVersion"

    implementation "org.glassfish.jaxb:jaxb-runtime"
    implementation "jakarta.xml.ws:jakarta.xml.ws-api"
    implementation "org.apache.cxf.xjc-utils:cxf-xjc-runtime:$jakartaVersion"
    implementation(files(generateStubs.wsdl2JavaOptions.outputDir).builtBy(generateStubs))

    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

idea {
    module {
        generatedSourceDirs += file("$projectDir/src/main/generated")
    }
}

bootJar {
    archiveFileName = "${project.name}.jar"
}
