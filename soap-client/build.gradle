apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "com.yupzip.wsdl2java"

wsdl2java {
    generatedWsdlDir = file("$projectDir/src/main/generated")
    wsdlsToGenerate = [
            ["-p","ru.romanow.protocols.soap.generated.literal.model",
             "$projectDir/src/main/resources/wsdl/document-literal.wsdl"],
            ["-p","ru.romanow.protocols.soap.generated.wrapped.model",
             "$projectDir/src/main/resources/wsdl/document-literal-wrapped.wsdl"],
            ["-p","ru.romanow.protocols.soap.generated.rpc.model",
             "$projectDir/src/main/resources/wsdl/rpc-literal.wsdl"]
    ]
    cxfVersion = "$cxfVersion"
}

compileJava.dependsOn(wsdl2javaTask)

idea {
    module {
        generatedSourceDirs += file(wsdl2java.generatedWsdlDir)
    }
}

sourceSets {
    main {
        java {
            srcDirs "src/main/java", wsdl2java.generatedWsdlDir
        }
    }
}

dependencies {
    implementation project(":soap-protocol")

    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-logging"

    wsdl2java "com.sun.xml.bind:jaxb-xjc:$jaxbXjcVersion"
    wsdl2java "javax.xml.bind:jaxb-api:$jaxbApiVersion"
    wsdl2java "javax.xml.ws:jaxws-api:$jaxwsApiVersion"
    wsdl2java "org.apache.cxf:cxf-rt-wsdl:$cxfVersion"
    wsdl2java "javax.jws:javax.jws-api:$jwsApiVersion"

    implementation "com.sun.xml.bind:jaxb-xjc:$jaxbXjcVersion"
    implementation "javax.xml.bind:jaxb-api:$jaxbApiVersion"
    implementation "javax.xml.ws:jaxws-api:$jaxwsApiVersion"
    implementation "org.apache.cxf:cxf-rt-wsdl:$cxfVersion"
    implementation "javax.jws:javax.jws-api:$jwsApiVersion"
    implementation "com.sun.xml.ws:rt:$xmlWsVersion"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

bootJar {
    archiveFileName = "${project.name}.jar"
}